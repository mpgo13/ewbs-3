%%%%% windmill output %%%%%

wind_ok(C) :- control(C), c(C, i4, Wind), Wind >= 5, Wind <= 40.
wind_nok(C) :- control(C), c(C, i4, Wind), Wind < 5.
wind_nok(C) :- control(C), c(C, i4, Wind), Wind > 40.

load_exceeds_or_equals_demand(C) :- load_equals_demand(C).
load_exceeds_or_equals_demand(C) :- load_exceeds_demand(C).
load_equals_demand(C) :- control(C), c(C, i1, Load), c(C, i2, Demand), Load == Demand.
load_exceeds_demand(C) :- control(C), c(C, i1, Load), c(C, i2, Demand), Load > Demand.
demand_exceeds_or_equals_load(C) :- demand_equals_load(C).
demand_exceeds_or_equals_load(C) :- demand_exceeds_load(C).
demand_equals_load(C) :- control(C), c(C, i1, Load), c(C, i2, Demand), Load == Demand.
demand_exceeds_load(C) :- control(C), c(C, i1, Load), c(C, i2, Demand), Load < Demand.

to_produce(C, X) :- control(C), c(C, i1, Load), c(C, i2, Demand), X = Demand - Load.

c(C, o2, 0) :- control(C), wind_nok(C), not ab(C).
c(C, o2, 0) :- control(C), wind_ok(C),
               load_exceeds_or_equals_demand(C),
               not ab(C).
c(C, o2, X) :- control(C), wind_ok(C),
               demand_exceeds_load(C),
               to_produce(C, X), c_max(W, MaxCap), windmill(W),
               X <= MaxCap,
               not ab(C).
c(C, o2, MaxCap) :- control(C), wind_ok(C),
                    demand_exceeds_load(C),
                    to_produce(C, X), c_max(W, MaxCap), windmill(W),
                    X > MaxCap,
                    not ab(C).

%%%%% pumped-storage output %%%%%

psh_empty(C) :- control(C), c(C, i3, empty).
psh_nempty(C) :- control(C), c(C, i3, half_full).
psh_nempty(C) :- control(C), c(C, i3, full).

psh_full(C) :- control(C), c(C, i3, full).
psh_nfull(C) :- control(C), c(C, i3, half_full).
psh_nfull(C) :- control(C), c(C, i3, empty).

c(C, o1, 0) :- control(C),
               psh_empty(C),
               demand_exceeds_or_equals_load(C),
               not ab(C).

% charge pumped-storage

c(C, o1, charge) :- control(C), psh_nfull(C),
                    load_exceeds_demand(C),
                    not ab(C).
c(C, o4, 2) :- control(C), psh_nfull(C),
               load_exceeds_demand(C),
               not ab(C).
c(C, o5, P) :- control(C), psh_nfull(C),
               load_exceeds_demand(C),
               c(C, i1, Load), c(C, i2, Demand), w_charge(MaxCap),
               P = Load - Demand, P <= MaxCap,
               not ab(C).
c(C, o5, MaxCap) :- control(C), psh_nfull(C),
                    load_exceeds_demand(C),
                    c(C, i1, Load), c(C, i2, Demand), w_charge(MaxCap),
                    P = Load - Demand, P > MaxCap,
                    not ab(C).

c(C, o1, 0) :- control(C), psh_full(C),
               load_exceeds_demand(C),
               not ab(C).

c(C, o1, 0) :- control(C), load_equals_demand(C),
               not ab(C).

c(C, o1, Z) :- control(C), psh_nempty(C),
               demand_exceeds_load(C),
               to_produce(C, X), c(C, o2, Y), c_max(P, MaxCap), storage_plant(P),
               Z = X - Y, Z <= MaxCap,
               not ab(C).
c(C, o1, MaxCap) :- control(C), psh_nempty(C),
                    demand_exceeds_load(C),
                    to_produce(C, X), c(C, o2, Y), c_max(P, MaxCap), storage_plant(P),
                    Z = X - Y, Z > MaxCap,
                    not ab(C).

%%%%% fossil fuel output %%%%%

to_produce_f(Z) :- control(C), c(C, o1, A), c(C, o2, B), to_produce(C, X),
                   Y = X - A, Z = Y - B.

c(C, o3, 0) :- control(C), load_exceeds_or_equals_demand(C), not ab(C).

c(C, o3, Z) :- control(C), demand_exceeds_load(C),
               to_produce_f(Z), c_max(F, MaxCap), caloric_plant(F),
               Z < MaxCap,
               not ab(C).

c(C, o3, MaxCap) :- control(C), demand_exceeds_load(C),
                    to_produce_f(Z), c_max(F, MaxCap), caloric_plant(F),
                    Z >= MaxCap,
                    not ab(C).

%%%%% switching output %%%%%

c(C, o4, 1) :- control(C), c(C, o1, X), X > 0, not ab(C).
c(C, o4, 1) :- control(C), c(C, o2, X), X > 0, not ab(C).
c(C, o4, 1) :- control(C), c(C, o3, X), X > 0, not ab(C).

c(C, o4, 0) :- control(C), c(C, o1, 0), c(C, o2, 0), c(C, o3, 0), not ab(C).

c(C, o5, 0) :- control(C), psh_full(C), not ab(C).
c(C, o5, 0) :- control(C), demand_exceeds_load(C), not ab(C).
c(C, o5, 0) :- control(C), load_equals_demand(C), not ab(C).
